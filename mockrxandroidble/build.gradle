apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
apply plugin: 'com.vanniktech.maven.publish'
apply from: rootProject.file('gradle/gradle-mvn-config.gradle')

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file

def getVersionName = { ->
    return rootProject.ext.publishingVersionName// Replace with version Name
}

def getArtificatId = { ->
    return "mockrxandroidble" // Replace with library name ID
}

android {
    namespace 'com.polidea.rxandroidble2.mockrxandroidble'

    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 32
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    lint {
        abortOnError false
    }

    preBuild.dependsOn 'checkstyle'

    sourceSets {
        test {
            java {
                srcDir("../rxandroidble/src/test/java") // Add android stubs from main lib tests
            }
        }
    }

    publishing {
        publications {
            gpr(MavenPublication) {
                groupId 'com.hiorka' // Replace with group ID
                artifactId getArtificatId()
                version getVersionName()
                artifact("$buildDir/outputs/aar/${getArtificatId()}-release.aar")
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Evocolabs/mockrxandroidble")
                credentials {
                    username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                    password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                }
            }
        }
    }
}

afterEvaluate { project ->
    // add explicit dependencies to compile groovy when testing
    testDebugUnitTest.dependsOn 'compileDebugUnitTestGroovyWithGroovyc'
    testReleaseUnitTest.dependsOn 'compileReleaseUnitTestGroovyWithGroovyc'
}

dependencies {

    // Test dependencies
    testImplementation rootProject.ext.libs.junit
    testImplementation rootProject.ext.libs.groovy
    testImplementation rootProject.ext.libs.spock
    api project(path: ':rxandroidble')
    api rootProject.ext.libs.rxjava
}
