apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
//apply plugin: 'com.vanniktech.maven.publish'
apply from: rootProject.file('gradle/gradle-mvn-config.gradle')
apply plugin: 'maven-publish'


def getVersionName = { ->
    return "1.1.3" // Replace with version Name
}

def getArtificatId = { ->
    return "rxandroidble" // Replace with library name ID
}

android {
    namespace 'com.polidea.rxandroidble2'

    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 33
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    lint {
        abortOnError false
    }

    preBuild.dependsOn 'checkstyle'

    packagingOptions {
        // Dagger 2.25.4 introduced a file for tracking purposes. This can break apps using Dagger.
        // See https://github.com/dariuszseweryn/RxAndroidBle/issues/789
        // Source https://github.com/google/dagger/commit/709098caaf4c7124f5e5313c1aa9ab34fced0031
        excludes = ['**/com.google.dagger_dagger.version']
    }
}

androidGroovy {
    skipJavaC = true
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'com.hiorka' // Replace with group ID
            artifactId getArtificatId()
            version getVersionName()

            afterEvaluate {
                from components.release
            }
        }
    }

    repositories {
        maven {
            def properties = new Properties()
            properties.load(new FileInputStream(rootProject.file("aws.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file
            url 'https://orka-085552102522.d.codeartifact.us-west-2.amazonaws.com/maven/orka-device-sdk/'
            credentials {
                username "aws"
                password properties['token']
            }
        }
    }
}

dependencies {
    api rootProject.ext.libs.rxjava
    api rootProject.ext.libs.annotations
    implementation rootProject.ext.libs.rxrelay
    implementation files(tasks.getByPath(':dagger-library-shadow:shadowJar').archiveFile)
    implementation 'com.google.android.things:androidthings:1.0'
    annotationProcessor project(path: ':dagger-compiler-shadow', transitive: false, configuration: 'shadow')

    // Test dependencies
    testImplementation files(tasks.getByPath(':dagger-library-shadow:shadowJar').archiveFile)
    testImplementation rootProject.ext.libs.junit
    testImplementation rootProject.ext.libs.groovy
    testImplementation rootProject.ext.libs.spock
}
