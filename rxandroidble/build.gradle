apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
//apply plugin: 'com.vanniktech.maven.publish'
apply from: rootProject.file('gradle/gradle-mvn-config.gradle')
apply plugin: 'maven-publish'


def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file

def getVersionName = { ->
    return "1.0.5" // Replace with version Name
}

def getArtificatId = { ->
    return "rxandroidble" // Replace with library name ID
}

android {
    namespace 'com.polidea.rxandroidble2'

    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 32
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    lint {
        abortOnError false
    }

    preBuild.dependsOn 'checkstyle'

    packagingOptions {
        // Dagger 2.25.4 introduced a file for tracking purposes. This can break apps using Dagger.
        // See https://github.com/dariuszseweryn/RxAndroidBle/issues/789
        // Source https://github.com/google/dagger/commit/709098caaf4c7124f5e5313c1aa9ab34fced0031
        excludes = ['**/com.google.dagger_dagger.version']
    }
}

androidGroovy {
    skipJavaC = true
}

publishing {
    publications {
        gpr(MavenPublication) {
            groupId 'com.hiorka' // Replace with group ID
            artifactId getArtificatId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/${getArtificatId()}-release.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Evocolabs/rxandroidble")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}
//}

dependencies {
    api rootProject.ext.libs.rxjava
    api rootProject.ext.libs.annotations
    implementation rootProject.ext.libs.rxrelay
    implementation files(tasks.getByPath(':dagger-library-shadow:shadowJar').archiveFile)
    annotationProcessor project(path: ':dagger-compiler-shadow', transitive: false, configuration: 'shadow')

    // Test dependencies
    testImplementation files(tasks.getByPath(':dagger-library-shadow:shadowJar').archiveFile)
    testImplementation rootProject.ext.libs.junit
    testImplementation rootProject.ext.libs.groovy
    testImplementation rootProject.ext.libs.spock
}
